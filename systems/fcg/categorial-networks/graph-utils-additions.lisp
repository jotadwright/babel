;; Copyright 2019 AI Lab, Vrije Universiteit Brussel - Sony CSL Paris

;; Licensed under the Apache License, Version 2.0 (the "License");
;; you may not use this file except in compliance with the License.
;; You may obtain a copy of the License at

;;     http://www.apache.org/licenses/LICENSE-2.0

;; Unless required by applicable law or agreed to in writing, software
;; distributed under the License is distributed on an "AS IS" BASIS,
;; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
;; See the License for the specific language governing permissions and
;; limitations under the License.
;;=========================================================================

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Additions to graph-utils ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(in-package :graph-utils)

(export '(find-cheapest-path find-undirected-path
          find-weight-of-path coherence
          connected?
          neighbours?))

(defgeneric find-cheapest-path (graph node-1 node-2)
  (:documentation "Dijkstra's algorithm for finding the shortest path between two nodes."))
            
(defmethod find-cheapest-path ((graph undirected-typed-graph) (n1 integer) (n2 integer))
  (let ((nodes (node-ids graph)))
    (let ((distances (mapcar (lambda (n) (cons n most-positive-fixnum)) nodes))
          (previous (mapcar (lambda (n) (cons n nil)) nodes)))
      (setf (cdr (assoc n1 distances)) 0)
      (loop until (null nodes) do
            (setf distances (sort distances '< :key 'cdr))
            (let ((next (first (remove-if-not (lambda (d)
                                                (member (car d) nodes))
                                              distances))))
              (when (= (cdr next) most-positive-fixnum)
                (return nil))
              (when (= (car next) n2)
                (return-from find-cheapest-path
                  (nreverse (reconstruct-path previous n2))))
              (setq nodes (remove (car next) nodes))
              (dolist (edge-type-and-neighbor
                       (if (directed? graph)
                         (outbound-neighbors graph (car next))
                         (neighbors graph (car next))))
                (let* ((neighbor (cdr edge-type-and-neighbor))
                       (distance (+ (cdr (assoc (car next) distances)) (edge-weight graph (car next) neighbor))))
                  (when (< distance (cdr (assoc neighbor distances)))
                    (setf (cdr (assoc neighbor distances)) distance
                          (cdr (assoc neighbor previous)) (car next))))))))))

(defmethod find-cheapest-path ((graph undirected-typed-graph) n1 n2)
  (find-cheapest-path graph
		      (gethash n1 (nodes graph))
		      (gethash n2 (nodes graph))))

(defgeneric find-weight-of-path (graph path)
  (:documentation "Returns the total weight of a path."))

(defmethod find-weight-of-path ((graph undirected-typed-graph) path)
  (loop for (n1 n2) in path summing (edge-weight graph n1 n2)))

(defgeneric find-undirected-path (graph node1 node-2)
  (:documentation "Returns any path between two nodes, doesn't take into account direction of links"))

(defmethod find-undirected-path ((graph undirected-typed-graph) (n1 integer) (n2 integer))
  (let* ((nodes (node-ids graph)))
    (let ((distances (mapcar (lambda (n) (cons n most-positive-fixnum)) nodes))
	  (previous (mapcar (lambda (n) (cons n nil)) nodes)))
      (setf (cdr (assoc n1 distances)) 0)
      (loop until (null nodes) do
            (setf distances (sort distances '< :key 'cdr))
            (let ((next (first (remove-if-not (lambda (d)
                                                (member (car d) nodes))
                                              distances))))
              (when (= (cdr next) most-positive-fixnum)
                (return nil))
              (when (= (car next) n2)
                (return-from find-undirected-path
                  (nreverse (reconstruct-path previous n2))))
              (setq nodes (remove (car next) nodes))
              (dolist (edge-type-and-neighbor (neighbors graph (car next)))
                (let* ((neighbor (cdr edge-type-and-neighbor))
                       (distance (1+ (cdr (assoc (car next) distances)))))
                  (when (< distance (cdr (assoc neighbor distances)))
                    (setf (cdr (assoc neighbor distances)) distance
                          (cdr (assoc neighbor previous)) (car next))))))))))

(defmethod find-undirected-path ((graph undirected-typed-graph) (n1 symbol) (n2 symbol))
  "finds nodes by node-ids and calls method for that."
  (find-undirected-path graph
                        (gethash n1 (nodes graph))
                        (gethash n2 (nodes graph))))

(defmethod coherence ((graph undirected-typed-graph))
  (let ((in-node-counter 0)
        (out-node-counter 0))
    (loop with matrix = (matrix (matrix graph))
          for node being the hash-keys of matrix
          do
          (incf in-node-counter)
          (loop with outgoing-edges =  (gethash node matrix)
                for out-node being the hash-keys of outgoing-edges
                when (< (gethash out-node outgoing-edges) 1.0)
                do
                (incf out-node-counter)))
    (if (> out-node-counter 0)
      (/ out-node-counter in-node-counter)
      1)))

(defmethod connected? ((graph undirected-typed-graph) (n1 integer) (n2 integer))
  ;; Check if there is a path from n1 to n2 using BFS
  (let* ((start-node n1)
         (dest-node n2)
         (queue (list start-node))
         (visited (list start-node)))
    (loop while queue
          for node = (pop queue)
          if (eq node dest-node)
          do (return t)
          else do
          (let ((neighbor-nodes (neighbors graph node)))
            (loop for (nil . neighbor-node) in neighbor-nodes
                  unless (member neighbor-node visited)
                  do
                  (setf queue (append queue (list neighbor-node)))
                  (setf visited (append visited (list neighbor-node))))))))

(defmethod connected? ((graph undirected-typed-graph) n1 n2)
  (connected? graph
              (gethash n1 (nodes graph))
              (gethash n2 (nodes graph))))


(defmethod neighbours? ((graph undirected-typed-graph) (n1 integer) (n2 integer) &key edge-type)
  (let ((matrix (gethash edge-type (matrix graph))))
    (when (and (sparse-array? matrix)
               (numberp (saref matrix n1 n2)))
      (second
       (multiple-value-list
        (saref matrix n1 n2))))))

(defmethod neighbours? ((graph undirected-typed-graph) n1 n2 &key edge-type)
  (neighbours? graph
               (gethash n1 (nodes graph))
               (gethash n2 (nodes graph))
               :edge-type edge-type))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Type Hierarchy -> S-dot ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(export '(categorial-network->s-dot))

(defun rgb->rgbhex (rgb)
  "Converts a RGB [0,255] value to an 8-bit hexadecimal string."
  (format nil "#铂О佝磲疸狎＇灬礅溽蝻躅┅蜱猢┅ㄤ彐躅箬徜瀛镦珧狴ㄥ溏瀛麇殓梏⒛弭弪黹铄翳箬徜镦珧狴骘翳邃珏溴疱钿轭镱翳邃珏麇殓梏戾舄è蜱猸鲠í邃珏麇殓梏脖暴蜱祜镳蝈疱狒泔祆邈蜱猸鲠飑ㄨ屮蜱猸掘玮桢蜱猢┅桢┅ㄤ彐躅汜翦顼蜷犰铄赭矧氕铒溴倔滹铒溴钺礤铒溴殇脲泔祜蝮箝弩⒘铒溴轭翳豉疱栝弪狎汨蟓滹簪啜蟓滹艉侯镤ㄡ痧孱啜蟓滹艉洪ㄦ矧磲铋幄铒溴殇┅蟓滹艉红徕屐ㄦ矧磲铋狺铒溴钺礤┅蟓滹艉烘镱纛犴⑷屐鲥糸汜蟓滹艉烘镱趔辁⒈爱阿蟓滹艉烘殪煦镬矧ㄩㄨ狍璀翎忪瀛泔祜蝮ㄧ弭栳箬铒溴钺礤泔祜蝮Ｆ破瓢阿┅蟓滹艉后栳疱ㄩㄨ狍璀翎忪瀛箝弩⑩秫㈠祆轲箦┅蟓滹艉后豉戾㈡殪戾洧┅麒孱ㄨ狍璀翎忪瀛箝弩啜蟓滹艉瑚殇翳ㄦ矧磲铋脾ㄧ弭栳箬铒溴钺礤箝弩┅蟓滹艉烘轼邃箝Ⅳ蝓澧蟓滹艉烘镱趔辁ㄦ矧磲铋蘑趄躅汜翦í卑ㄧ弭栳箬铒溴钺礤箝弩┅┅┅┅┅ㄤ彐躅汜翦顼蜷犰铄赭矧氕邃珏倔滹ㄦ蝻麸脲麇殓梏溟蝈泗邃泔祜蝈洵邃珏蟓碍暴⒘邃珏轭翳豉疱栝弪狎汨蟓滹簪啜蟓滹艉哄溏è蟓滹艉烘蝻ㄦ矧磲铋幄骝镯┅蟓滹艉呼ㄦ矧磲铋幄麸┅蟓滹艉轰轵ㄩ溟蝈泗邃㈡矧麽蜾㈩镱澧┅蟓滹艉烘镱趔辁⒈爱阿蟓滹艉红徕屐ㄩ麇殓梏ㄦ矧磲铋叉麇殓梏┅蟓滹艉烘镱翥镬矧ㄩㄡ钿泔祜蝈洵邃珏蟓碍麇殓梏箬徜瀛镦珧狴麇殓梏┗筱犰仪珧狴忮赭邋ò艾癌犷ú北脖爆脖暴０鞍鞍阿┅蟓滹艉恒镬矧ㄩㄡ钿泔祜蝈洵邃珏蟓碍麇殓梏箬徜瀛镦珧狴麇殓梏０鞍鞍阿┅蟓滹艉后豉戾ㄩㄡ钿泔祜蝈洵邃珏蟓碍麇殓梏ㄣ镱è犷麇殓梏爱暴窘麇殓梏爱癌⑩镬洧è犷冀麇殓梏暴麇殓梏爱供滹趑邃㈡殪戾洧┅㈡殪戾洧┅┅ㄤ彐礤翳镤汜翦顼蜷犰铄赭矧氕倔滹è珧狃躅溟蝈泗邃豉疱洵珧狃瑭脲麇殓梏罂泔祜蝮箝弩泔祜蝈洵邃珏蟓碍暴⑼犭滹蝈痱弩孱翎糸镱镦翳珧狃躞轭蟓滹舢麇殓梏罂箬秣翳麇殓梏镱翳邃珏矧铒舢泔祜蝮栳箬翎忪翳狒篝矧弩泔祜骘遽汨铒溴箝弩栳箬翎忪翳狒篝矧弩箝骘遽汨铒溴泔祜蝈洵邃珏蟓碍躞箬徜弩镦珧狴犷邃珏篝戾麸轭溟汜翦殒翳麇殓梏镦翳邃珏轶沆矬麸矧雹戾舄è珧狃璀痱镳弪糸弩Ж蟓滹艉烘镱翥镬矧０鞍鞍阿蟓滹艉烘镱趔辁⒈爱阿蟓滹艉烘镱纛犴⑷屐鲥糸汜蟓滹艉候犷脘轵⑻尧┅ㄡ祆铒溴钺礤ㄧ蜥痂豸殪蠛红轶舡铒溴珧狃瑭ㄡ祆铒溴殇祜镳骘铒溴钺礤轭犰飙铒溴钺礤骘殇ㄧ弭栳箬铒溴钺礤ㄧ蜥痂豸殪蠛侯镤弩珧狃瑭泔祆邈殇┅ㄡ祆邃珏ㄧ蜥痂豸殪蠛红轶舡邃珏珧狃侯镤弩狍殇舂蟓滹舡铒溴祜镳骘铒溴钺礤轭犰飙铒溴钺礤骘铒溴殇轭犰飙铒溴殇泔祆邈ㄣ狒彗矧獒飙铄赭矧氕铒溴倔滹铒溴钺礤铒溴殇恒镬矧泔祜蝮后辁弩箝弩┅蟓滹舡邃珏换麇狎轭犷躅溟蝈泗邃珧狃祜镳骘ㄦ蝻憝殇麸殇轭犰飙邃珏骘邃珏麇殓梏ㄧ蜥痂豸殪蠛哄溏瀛麇殓梏珧狃骝镯殇麸殇麒孱骝镯殇麸殇泔祆邈ㄣ狒彗矧獒飙铄赭矧氕邃珏倔滹骝镯殇麸殇瑚彘玷ㄩ麇殓梏罂邃珏麇殓梏铋飑轰轵邈翦漯ㄧ蜥痂豸殪蠛轰轵邈翦淇珧狃瑭恒镬矧邃邃珏蟓碍泔祜蝈洵邃珏蟓碍暴┅啜蟓滹艉虹蜥痂珧狃璀痱镳弪糸弩荔滹舡铒溴荔滹舡邃珏螬┅