(in-package :clevr-world)

(defparameter *stroke-width* "20")

(defun rgb->rgbhex (rgb)
  "Converts a RGB value to an 8-bit hexadecimal string."
  (format nil "#铂О佝蜱猢ㄤ彐躅沆弼颦泔祜颦掘玮ㄣ镬矧ㄣ狍泔祜ㄧ蜥Ж阜阜阜┅蝈Ж狈炒车┅ㄢ祯Ж创范脖旦ㄧ蝈孱Ж补卑舶┅ㄢ蝻黝Ж辈凡驳┅瘐蝠戾Ж背彻惫博ㄣ犷Ж窗舶舶俯屐祜Ж驳渤当┅┅换换＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃换换沆弼颦箬狃瀛倔鲧换换＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃ㄤ彐珏铄蜷沆弼颦箬狃瀛倔鲧ㄣ戾鲵镡赍泗箬狃濠ê滹沲礤铘狒轱⒛蜥黧沆弼颦镡赍泗镦翳玳鲥箬狃轭又洽┅换骘铒鳜翳磲翦蜷犰镦翳镡赍泗轶铒蝈骒邈翦轭翳又ㄤ彐礤翳镤沆弼颦箬狃瀛倔鲧è镡赍泗沆弼颦镡赍泗箬狃ㄥ耢箴桢蝈┅ㄤ邈灬蝈ㄩ珙矧徕戾箬狃濠戾舄è痫镡赍泗┅痫镡赍泗┅蜥溟躞ㄣ狍箝镡赍泗箜犰钡灬蜱嘲┅蜱ㄣ戾鲵泔祜颦掘玮ㄣ镬矧镡赍泗┅┅啜ㄧ呼蜥铙骘蝽黼篝Ⅳ蜥铙灬翦áㄦ矧磲铋辨ㄦ矧磲铋辨┅è汩蜚戾恒ㄦ矧磲铋叉蜥溟躞恒ㄦ矧磲铋叉癌候ㄦ矧磲铋叉蜥溟躞烘殪蜱猸掘玮桢蜱猢┅┅ㄤ彐礤翳镤沆弼颦箬狃瀛倔鲧è镡赍泗沆弼颦镡赍泗箬狃ㄥ耢с扉钿弪┅ㄤ邈灬蝈ㄩ珙矧徕戾箬狃濠戾舄è痫镡赍泗┅痫镡赍泗┅蜥溟躞ㄣ狍箝镡赍泗箜犰钡灬蜱嘲┅蜱ㄣ戾鲵泔祜颦掘玮ㄣ镬矧镡赍泗┅┅啜ㄧ呼蜥铙骘蝽黼篝Ⅳ蜥铙灬翦áㄦ矧磲铋辨ㄦ矧磲铋辨烘殪Ⅶ栝翦后趄镫蜱猸掘玮桢蜱猢后趄镫瀛鏖漪⒌è汩蜚戾恒ㄦ矧磲铋叉蜥溟躞恒ㄦ矧磲铋叉癌候ㄦ矧磲铋叉蜥溟躞┅┅┅ㄤ彐礤翳镤沆弼颦箬狃瀛倔鲧è镡赍泗沆弼颦镡赍泗箬狃ㄥ耢с踱濠┅ㄤ邈灬蝈ㄩ珙矧徕戾箬狃濠戾舄è痫镡赍泗┅痫镡赍泗┅鏖漪ㄣ狍箝镡赍泗箜犰嘲灬蜱栋┅蜱ㄣ戾鲵泔祜颦掘玮ㄣ镬矧镡赍泗┅┅啜ㄧ呼蜥铙骘蝽黼篝Ⅳ蜥铙灬翦áㄦ矧磲铋辨ㄦ矧磲铋辨Ⅱ雉狒濞ㄦ矧磲铋辨í拱ǒ蝻翎糸镱镡赍泗痖┅┅è蝈泗壶ㄦ矧磲铋叉í爱鏖漪瑭葫ㄦ矧磲铋叉í爱鏖漪瑭瑚殇翳ㄦ矧磲铋叉鏖漪瑭鸿彘玷ㄦ矧磲铋叉鏖漪瑭烘殪蜱猸掘玮桢蜱猢┅┅换换＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃换换磲脲篥换换＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃ㄤ彐珏铄蜷磲脲篥翳轭绌ê滹沲礤铘狒轱⒛蜥黧翳轭轭又洽┅ㄤ彐礤翳镤磲脲篥è镡赍泗沆弼颦镡赍泗┅ㄣ戾鲵箬狃瀛倔鲧镡赍泗箬狃镡赍泗┅ㄤ彐礤翳镤磲脲篥è筱孱沆弼颦筱孱濠啜ㄧ括祜镳骘镡赍泗轭镡赍泗筱孱濠泔祆邈磲脲篥镡赍泗┅┅换换＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃换换潋狩灬忮祗换换＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃ㄤ彐躅潋狩灬忮祗筱孱鏖漪脲箬秣舂ㄤ邈灬蝈ㄩ珙矧桢殓梏┅祜镳骘镡赍泗轭镡赍泗筱孱濠骘痫镡赍泗骘痫镡赍泗骘犰殓铐孱ㄩǒ鏖漪博Ⅱ殓梏㈧彐簪泔祆邈啜ㄤ轹恒灬篌㈠铘轸灬忮膦后豉戾ㄦ矧磲铋岷叉瘌霍镳湖叉瘌箕幄犰殓铐孱ㄩㄥ聃犰犰殓铐孱Ⅱ殓梏ō鏖漪ㄩ箬秣溟箴灬洪铎轭澧溟箴灬侯镱澧┅ㄨ繇飙痧蜷铘ㄩ镡赍泗┅┅换换＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃换换痱镧蜥憝倔滹换换＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃ㄤ彐躅忮趑弪骢钽糸镱钺礤钺礤ㄩ篝蜷铉钺礤Ⅲ沐铄㈢弭泔铘屮簪ㄩㄦ轭＼钺礤戾è疳螋箴扉钺礤＼擤┅ㄣ镱è矧篝蜷铉ㄦ轵篝疳螋螬㈡殪翦颌篝蜷铉ㄦ轵篝疳螋螬Ⅰ蹂蝙篝蜷铉ㄦ轵篝疳螋螬Ⅲ犴澧┅ㄦ轵篝疳螋螬è篝蜷铉ㄦ轵篝疳螋螬㈠聃犰ㄩ篝蜷铉箦泔钿疳螋螬㈤铘彗弪钺礤ㄦ轵篝疳螋螬┅钺礤┅钺礤┅ㄤ彐躅骢钽糸镱倔滹ㄦ躅泗轱瞟啜蟓滹艉候邈矧è蟓滹艉恒镬矧ｆ骀骀姊蟓滹艉烘镱趔辁⒏耽蟓滹艉烘镱纛犴＋矧瑚轭巢瑚轭滹黧⒂犷螈－矧瑚轭巢瑚轭滹黧⒘蜷犰蟓滹艉鸿彘玷爱氨┅蟓滹艉侯镤è蟓滹艉洪黼滹趔趄ㄩ骢钽糸镱┅蟓滹艉红徕屐黼滹趔趄ㄦ矧磲铋屺狺蕃)"
                                          (better-function-name (function-name function))
                                          (args function))))))))

(defun edge->s-dot (from to)
  `(s-dot::edge ((s-dot::from ,(mkdotstr (id from)))
                 (s-dot::to ,(mkdotstr (id to)))
                 (s-dot::dir "back")
                 (s-dot::arrowsize "0.5"))))

(defun program->s-dot (program)
  (let ((graph '(((s-dot::ranksep "0.3")
                  (s-dot::nodesep "0.5")
                  (s-dot::margin "0")
                  (s-dot::rankdir "LR"))
                 s-dot::graph)))
    (loop for node in (reverse (nodes program))
          do (push (function->s-dot node) graph))
    (loop for node in (reverse (nodes program))
          do (loop for child in (children node)
                   do (push (edge->s-dot node child) graph)))
    (reverse graph)))

;;;;; ############################################################
;;;;; make-html
;;;;; ############################################################

(defmethod make-html-for-entity-details ((object clevr-object) &key)
  `(((div :class "entity-detail") ,(format nil "size: a" (downcase (mkstr (size object)))))
    ((div :class "entity-detail") ,(format nil "color: a" (downcase (mkstr (color object)))))
    ((div :class "entity-detail") ,(format nil "material: a" (downcase (mkstr (material object)))))
    ((div :class "entity-detail") ,(format nil "shape: a" (downcase (mkstr (shape object)))))))

(defmethod make-html-for-entity-details ((q clevr-question) &key)
  (let ((expand-program-id (make-id)))
    `(((div :class "entity-detail")
       ,(make-expandable/collapsable-element
         expand-program-id (make-id)
         ;; collapsed
         `((a ,@(make-expand/collapse-link-parameters expand-program-id t)) "View Program")
         ;; expanded
         (lambda ()
           `((table)
             ((tr)
              ((td)
               ((a ,@(make-expand/collapse-link-parameters expand-program-id nil)) "Hide Program")))
             ((tr)
              ((td)
               ,(s-dot->svg (program->s-dot (program q)))))))))
      ((div :class "entity-detail")
       ((b) "Q: ") ((i) ,(mkstr (question q))))
      ((div :class "entity-detail" :style "text-align:right;")
       ((b) "A: ") ((i) ,(mkstr (answer q)))))))

(define-css 'clevr-scene "
div.clevr-scene {
   position:relative;overflow:hidden;
   argin-left:auto;margin-right:auto; }
div.clevr-scene > div.entity-label { 
   position:absolute; margin-top:-7px; margin-left:8px;margin-right:8px;
   padding:0px; background-color:#fff;display:inline;
   filter:alpha(opacity=70); -moz-opacity:.70; opacity:.70; }
div.clevr-scene > div.scene-title {
   position:absolute; padding:10px; background-color:#fff;top:10px;right:10px;  
   filter:alpha(opacity=70); -moz-opacity:.70; opacity:.70; }
")

(define-css 'clevr-scene-table "
div.clevr-scene-table {
    display:inline-block;position:relative;overflow:hidden;}
")

(defmethod make-html-for-entity-details ((set clevr-object-set) &key)
  ;; table of objects
  `(((div :class "entity-detail") 
     ,@(loop for object in (objects set)
             collect (make-html object :expand-initially t)))))

(defmethod make-html-for-entity-details ((scene clevr-scene)
                                         &key (width 500)
                                         (height 300)
                                         (show-labels t))
  (let ((expand-scene-id (make-id 'scene))
        (expand-all-id (make-id 'expand-all)))
    (append
     `(((div :class "clevr-scene-table")
        ,(make-expandable/collapsable-element
          expand-scene-id expand-all-id
          ;; collapsed
          `((table)
            ((tr)
             ((td)
              ((a ,@(make-expand/collapse-link-parameters expand-scene-id t)) "View Scene"))))
          ;; expanded
          (lambda ()
            `((table)
              ((tr)
               ((td)
                ((a ,@(make-expand/collapse-link-parameters expand-scene-id nil)) "Hide Scene")))
              ((tr)
               ((td) ,(format nil "a" (name scene))))
              ((tr)
               ((td :style "text-align:center;border:1px dotted #aaa;")
                ((div :class "clevr-scene"
                      :style ,(format nil  "width:,2fpx;height:,2fpx;" width height))
                 ;; add svg of wm
                 ((svg :xmlns "http://www.w3.org/2000/svg"
                       :width ,(format nil "apx" width)
                       :height ,(format nil "apx" height))
                  ((g)
                   ,(make-svg scene)))
                 ;; draw labels
                 ,@(draw-labels scene width :show show-labels)))))))))
     ;; table of objects
     `(((div :class "entity-detail") 
        ,@(loop for object in (objects scene)
                collect (make-html object :expand-initially t)))))))

(defmethod make-html-for-entity-details ((set clevr-question-set) &key)
  `(((div :class "entity-detail")
     ,@(loop for q in (questions set)
             collect (make-html q :expand-initially t)))))

